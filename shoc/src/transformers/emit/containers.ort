
function(FileNode n, TransformerPass p) emit_value::for_file -> FileNode does
	int i=0
	while i<n.statements.len do
		if -n.statements:get_Node(i):is_a("DeclExpr") do
			p:emit_value(n.statements:get_Node(i))
		done
		i+=1
	done
return n

function(BlockBody n, TransformerPass p) emit_value::for_blockbody -> BlockBody does
	int i=0
	while i<n.statements.len do
		p:emit_value(n.statements:get_Node(i))
		i+=1
	done
return n

function(FunctionDecl n, TransformerPass p) emit_value::for_func -> FunctionDecl does
	if p.proj.options:contains("debug_emit_meth") do
		printf("Emitting method %s\n", n.head:get_strname())
	done
	if -n.head.is_extern do
		int i=0
		p.proj:enter_method_named(n.head:get_strname())

		if p.proj.options:contains("funchooks") & -n.head:get_strname():startswith("orth$$internal$$hooks") do
			List hook_args=List::new()
			hook_args:append_GIRVar(GIRLiteral::emit_cstr(p.proj, n.head:get_strname()))
			GIRCall::emit(p.proj, GIRGetMethod::emit(p.proj, p.proj:get_Method("orth$$internal$$hooks$$function_enter_hook")), hook_args)
		done

		while i<n.body.len do
			p:emit_value(n.body:get_Node(i))
			i+=1
		done
		p.proj:exit_method()
	done
return n

function(TypeDecl n, TransformerPass p) emit_value::for_typedecl -> TypeDecl does
	int i=0
	while i<n.body.statements.len do
		if -n.body.statements:get_Node(i):is_a("DeclExpr") do
			p:emit_value(n.body.statements:get_Node(i))
		done
		i+=1
	done
return n
