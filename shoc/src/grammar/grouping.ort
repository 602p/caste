type GroupingExpr is packed
	NodeType type_
	Token anchor
	Node contents

	function(GroupingExpr node, int depth) GroupingExpr::print -> void does
		printf("GroupingExpr: ")
		node.contents:print(depth+1)
	return

	function(Node contents, Token anchor) GroupingExpr::create_raw -> GroupingExpr does
		GroupingExpr new = malloc(@sizeof(GroupingExpr)@)|GroupingExpr
		new.type_ = GroupingExpr_t
		new.contents=contents
		new.anchor=anchor
	return new

	function(EleList eles) GroupingExpr::create -> GroupingExpr does
	return GroupingExpr::create_raw(eles:get(1):n(), eles:get(0):t())

	function(EleList eles) GroupingExpr::match -> int does
		if (-eles:get(0).is_token):or(\
		     eles:get(1).is_token):or(\
		    -eles:get(2).is_token) do
			return 0
		done

		if (eles:get(0):t().type_.name!="T_PAREN_OPEN"):or(\
			eles:get(2):t().type_.name!="T_PAREN_CLOSE"):or(\
		   -eles:get(1):n():is_a("ValueExpression")) do
			return 0
		done

		if parse::lookahead_is_possibly_function(eles) do
			return 0
		done
	return 3

	function(GroupingExpr n, TransformerPass p) GroupingExpr::visit -> void does
		n.contents=p:visit(n.contents)
	return

	function(GroupingExpr self) GroupingExpr::copy -> Node does
	return GroupingExpr::create_raw(self.contents:copy(), self.anchor)|Node

	function() GroupingExpr::impl -> void does
		GroupingExpr_t = NodeType::new("GroupingExpr")\
					:set_printer(GroupingExpr::print |NodePrinter)\
					:set_creator(GroupingExpr::create|NodeCreator)\
					:set_matcher(GroupingExpr::match |NodeMatcher)\
					:set_visitor(GroupingExpr::visit |NodeVisitor)\
					:set_tcopier(GroupingExpr::copy  |NodeTCopier)\
					:matches(3):addtype("Tuple"):addtype("ValueExpression")
	return
endtype

NodeType GroupingExpr_t