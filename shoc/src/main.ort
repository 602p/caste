#"SHOC STARTS HERE"
import std
import fileio
import args
import stdio
import err
import profile
import mem
import os

import shoc

function(int nargs, ArgList args) main -> int does
	mem::init_arena_allocator(1024*1024)

	orth::install_sigsev_handler()

	stdio::init()
	if nargs<2 do
		orth::fail("E: Bad args\n")
	done

	List search_path=List::new()
	search_path:append(".")

	StrMap options = StrMap::new()

	int i=1
	cstr arg
	while i<nargs do
		arg=args:get(i)

		if arg:startswith("-s") do
			search_path:append(arg:offset(2))
		done

		if arg:startswith("-f") do
			options:set(arg:offset(2), "1")
		done

		i+=1
	done

	if options:contains("profile"):or(options:contains("profile_dump_csv")) do
		orth::internal::profile::install_profiler()
	done

	File fd = File::open(args:get(1), "r")
	cstr contents=fd:readall()
	fd:close()

	Project proj = shoc::get_empty_project()\
	                    :with_base_tokens()\
	                    :with_base_grammar()\
	                    :with_base_passes()\
	                    :with_search_path(search_path)\
	                    :with_options(options)\

	Node node = proj:process_text(contents)

	node:print_root()

	if options:contains("dump_node_types") do
		printf("\n\n\n")
		proj:show_types()
	done

	if options:contains("profile_dump_csv") do
		orth::internal::profile::dump_profiling_data_csv("profile.csv")
	done

	mem::clean_up()
return 0