import "llvm/emitter.ort"
import "llvm/type_impls.ort"
import "llvm/emitters/method.ort"
import "llvm/emitters/literal.ort"
import "llvm/emitters/return.ort"
import "llvm/emitters/get.ort"
import "llvm/emitters/load.ort"
import "llvm/emitters/store.ort"
import "llvm/emitters/binop.ort"
import "llvm/emitters/cast.ort"
import "llvm/emitters/call.ort"
import "llvm/emitters/label.ort"
import "llvm/emitters/jump.ort"
import "llvm/emitters/not.ort"

function(GIRVar var) llvm::make_local_handle -> cstr does
return "%t_"+var.ssa_id:str()

function(Project p, cstr filename, bool verbose) Project:emit_as_llvm -> Project does
	File out = File::open(filename, "w")
	LLVMEmitter context = LLVMEmitter::new(out, p)\
	                                  :register_emitter("literal",    llvm::emit_literal     |LLVMGIREmitter)\
	                                  :register_emitter("return",     llvm::emit_return      |LLVMGIREmitter)\
	                                  :register_emitter("get_local",  llvm::emit_get_local   |LLVMGIREmitter)\
	                                  :register_emitter("get_method", llvm::emit_get_method  |LLVMGIREmitter)\
	                                  :register_emitter("get_global", llvm::emit_get_global  |LLVMGIREmitter)\
	                                  :register_emitter("load",       llvm::emit_load        |LLVMGIREmitter)\
	                                  :register_emitter("store",      llvm::emit_store       |LLVMGIREmitter)\
	                                  :register_emitter("binop",      llvm::emit_binop       |LLVMGIREmitter)\
	                                  :register_emitter("cast",       llvm::emit_cast        |LLVMGIREmitter)\
	                                  :register_emitter("cast_ptr",   llvm::emit_ptr_cast    |LLVMGIREmitter)\
	                                  :register_emitter("call",       llvm::emit_call        |LLVMGIREmitter)\
	                                  :register_emitter("label",      llvm::emit_label       |LLVMGIREmitter)\
	                                  :register_emitter("jump",       llvm::emit_jump        |LLVMGIREmitter)\
	                                  :register_emitter("not",        llvm::emit_not         |LLVMGIREmitter)

	int j=0
	OType typ

	context:emitl("@_the_zero_double = global double 0.0")

	while j<p.types:num_keys() do
		llvm::define_pass1(context, p.types:get_by_num(j)|OType)
		j+=1
	done

	j=0
	while j<p.types:num_keys() do
		llvm::define_pass2(context, p.types:get_by_num(j)|OType)
		j+=1
	done

	j=0
	while j<p.types:num_keys() do
		llvm::define_pass2_point_5(context, p.types:get_by_num(j)|OType)
		j+=1
	done

	j=0
	while j<p.types:num_keys() do
		llvm::define_pass3(context, p.types:get_by_num(j)|OType)
		j+=1
	done

	j=0
	TypedVar t
	while j<p.globals:num_keys() do
		t=p.globals:get_by_num(j)|TypedVar
		context:emit("@global_"+t.name+" = global "+context:get_ty(t.type_)+" ")
		if (t.type_.variety==S_INT)+(t.type_.variety==U_INT) do
			if (t.name=="shoc_heritage") & (-p.options:contains("no_bc_increase")) do
				context:emitl((shoc_heritage+1):str())
			else do
				context:emitl(p.global_inits:get(t.name))
			done
		else do
			context:emitl("null")
		done
		j+=1
	done

	ProgressBar bar
	if verbose do
		bar = ProgressBar::new(p.methods:num_keys(), 40, "Emitting.....")
	done

	j=0
	Method meth
	while j<p.methods:num_keys() do
		if verbose do
			bar:update(j+1)
		done
		llvm::emit_method(p.methods:get_by_num(j)|Method, context)
		j+=1
	done

	context:emit_cached_globals()

	if p.options:contains("dump_llvm_mappings") do
		llvm::dump_debug(context)
	done

	out:close()
return p

function(LLVMEmitter context) llvm::dump_debug -> void does
	printf("\n===LLVM EMITTER INFO===\n")
	int a=0
	int b
	printf("\nLocals Mappings:\n")
	while a<context.locals_maps:num_keys() do
		b=0
		printf(" Mapping for function: %s\n", context.locals_maps:get_key_by_num(a))
		while b<(context.locals_maps:get(context.locals_maps:get_key_by_num(a))|StrMap):num_keys() do
			printf("\t%s -> %s\n", (context.locals_maps:get(context.locals_maps:get_key_by_num(a))|StrMap):get_key_by_num(b),\
				                   (context.locals_maps:get(context.locals_maps:get_key_by_num(a))|StrMap):get_by_num(b))
			b+=1
		done
		a+=1
	done

	a=0
	printf("\nSSA Mappings:\n")
	while a<context.ssa_map:num_keys() do
		printf("\t%s -> %s\n", context.ssa_map:get_key_by_num(a), context.ssa_map:get_by_num(a))
		a+=1
	done

	a=0
	printf("\nType Mappings:\n")
	while a<context.types_map:num_keys() do
		printf("\t%s -> %s\n", context.types_map:get_key_by_num(a), context.types_map:get_by_num(a))
		a+=1
	done
return