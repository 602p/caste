
type LLVMGIREmitter is a function->cstr #(GIRInst, LLVMEmitter)

type LLVMEmitter is
	StrMap gir_emitters
	StrMap locals_maps
	StrMap ssa_map
	StrMap types_map
	File   fd
	cstr   current_method
	int    ssa_count
	List   cached_globals

	function(File fd) LLVMEmitter::new -> LLVMEmitter does
		LLVMEmitter new = malloc(@sizeof(LLVMEmitter)@)|LLVMEmitter
		new.gir_emitters=StrMap::new()
		new.locals_maps=StrMap::new()
		new.ssa_map=StrMap::new()
		new.types_map=StrMap::new()
		new.cached_globals=List::new()
		new.ssa_count=0

		new.types_map:set("int", "i32")
		new.types_map:set("short", "i16")
		new.types_map:set("byte", "i8")
		new.types_map:set("long", "i64")
		new.types_map:set("xlong", "i128")
		
		new.types_map:set("uint", "i32")
		new.types_map:set("ushort", "i16")
		new.types_map:set("ubyte", "i8")
		new.types_map:set("ulong", "i64")
		new.types_map:set("uxlong", "i128")

		new.types_map:set("bool", "i1")

		new.types_map:set("float", "double")

		new.types_map:set("ptr", "i8*")
		new.types_map:set("cstr", "i8*")

		new.types_map:set("void", "void")

		new.fd=fd
		new.current_method="???"
	return new

	function(LLVMEmitter self, cstr text) LLVMEmitter:cache_global_stmt -> void does
		self.cached_globals:append(text)
	return

	function(LLVMEmitter self) LLVMEmitter:emit_cached_globals -> void does
		int i=0
		while i<self.cached_globals.len do
			self:emitl(self.cached_globals:get(i))
			i+=1
		done
	return

	function(LLVMEmitter self) LLVMEmitter:get_next_ssa -> int does
		self.ssa_count+=1
	return self.ssa_count

	function(LLVMEmitter self, cstr gir_type, LLVMGIREmitter e) LLVMEmitter:register_emitter -> LLVMEmitter does
		self.gir_emitters:set(gir_type, e|ptr)
	return self

	function(LLVMEmitter self, cstr gir_type) LLVMEmitter:get_emitter -> LLVMGIREmitter does
	return self.gir_emitters:get(gir_type)|LLVMGIREmitter

	function(LLVMEmitter self, GIRInst i) LLVMEmitter:emit_inst -> void does
		self:get_emitter(i._gir_ty.name)(i, self)
	return

	function(LLVMEmitter self, cstr text) LLVMEmitter:emit -> void does
		self.fd:write(text)
		# printf("LLVM Emit :%s\n", text)
	return

	function(LLVMEmitter self, cstr text) LLVMEmitter:emitl -> void does
		self.fd:write(text)
		self.fd:write("\n")
		# printf("LLVM Emitl:%s\n", text)
	return

	function(LLVMEmitter self, cstr name) LLVMEmitter:enter_method -> LLVMEmitter does
		self.current_method=name
		self.locals_maps:set(name, StrMap::new()|ptr)
	return self

	function(LLVMEmitter self, cstr name) LLVMEmitter:get_local_addr_var -> cstr does
	return (self.locals_maps:get(self.current_method)|StrMap):get(name)

	function(LLVMEmitter self, cstr name, cstr llvm_name) LLVMEmitter:set_local_addr_var -> void does
		(self.locals_maps:get(self.current_method)|StrMap):set(name, llvm_name)
	return

	function(LLVMEmitter self, GIRVar name) LLVMEmitter:get_ssa_var -> cstr does
	return self.ssa_map:get(name:str()) #TODO:Horrible!

	function(LLVMEmitter self, GIRVar name, cstr llvm_name) LLVMEmitter:set_ssa_var -> void does
		self.ssa_map:set(name:str(), llvm_name)
	return

	function(LLVMEmitter self, GIRVar var) LLVMEmitter:get_vty -> cstr does
		if var.is_ref do
			return (self.types_map:get(var.type_.name)|cstr)+"*"
		done
	return self.types_map:get(var.type_.name)

	function(LLVMEmitter self, OType ty) LLVMEmitter:get_ty -> cstr does
	return self.types_map:get(ty.name)
endtype
