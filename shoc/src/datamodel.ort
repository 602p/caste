int VOID #0
int S_INT #1
int U_INT #2
int FLOAT #3
int POINTER #4
int F_PTR #5
int STRUCT #6
int PTYPE #7
int SYMTYPE #8

function(int variety) datamodel::variety_to_name -> cstr does
	if variety==0 do
		return "VOID"
	elif variety==1 do
		return "S_INT"
	elif variety==2 do
		return "U_INT"
	elif variety==3 do
		return "FLOAT"
	elif variety==4 do
		return "POINTER"
	elif variety==5 do
		return "F_PTR"
	elif variety==6 do
		return "STRUCT"
	elif variety==7 do
		return "PTYPE"
	done
	shoc::bail_out("datamodel::variety_to_name called on invalid variety: "+variety:str())
return ""

type TypedVar is
	cstr name
	OType type_

	function(cstr name, OType type_) TypedVar::new -> TypedVar does
		TypedVar new = malloc(@sizeof(TypedVar)@)|TypedVar
		new.name=name
		new.type_=type_
	return new
endtype

function(List l, TypedVar v) List:append_TypedVar -> void does
	l:append(v|ptr)
return

function(List l, OType v) List:append_OType -> void does
	l:append(v|ptr)
return

function(List l, int i) List:get_TypedVar -> TypedVar does
return l:get(i)|TypedVar

function(List l, int i) List:get_OType -> OType does
return l:get(i)|OType

type OType is packed
	int variety
	cstr name
	Project proj

	function(OType t) OType:is_primitive -> bool does
	return t.variety<5

	function(OType t) OType:is_fptr -> bool does
	return t.variety==5

	function(OType t) OType:is_struct -> bool does
	return t.variety==6

	function(OType t) OType:is_sym -> bool does
	return t.variety==SYMTYPE

	function(OType t) OType:as_primitive -> PrimitiveOType does
	return t|PrimitiveOType

	function(OType t) OType:as_struct -> StructOType does
	return t|StructOType

	function(OType t) OType:as_fptr -> FPtrOType does
	return t|FPtrOType

	function(OType t) OType:as_sym -> SymType does
	return t|SymType

	function(OType t) OType:get_size -> int does
		if t:is_primitive() do
			return t:as_primitive():get_size()
		elif t:is_fptr() do
			return t:as_fptr():get_size()
		done
	return t:as_struct():get_size()

	function(OType t, Project p) OType:set_project -> OType does
		t.proj=p
	return t

	function(OType t) OType:print -> void does
		if t:is_primitive() do
			t:as_primitive():print()
		elif t:is_fptr() do
			t:as_fptr():print()
		elif t:is_struct() do
			t:as_struct():print()
		elif t:is_sym() do
			t:as_sym():print()
		elif t.variety==PTYPE do
			t|PType:print()
		else do
			printf("??? %i\n", t.variety)
		done
	return

	function(OType self, OType other) OType:__eq__ -> bool does
	return self|ptr == other|ptr

	function(OType self, OType other) OType:__ne__ -> bool does
	return self|ptr != other|ptr

	function(OType self, GenericArgs params) OType:resolve -> OType does
		if self.variety==PTYPE do
			return self|PType:resolve(params)
		elif self.variety==SYMTYPE do
			return params:get_name(self|SymType.name)
		done
	return self
endtype

type PrimitiveOType is packed
	int variety
	cstr name
	Project proj
	int size

	function(cstr name, int v, int size) PrimitiveOType::new -> OType does
		PrimitiveOType new = malloc(@sizeof(PrimitiveOType)@)|PrimitiveOType

		new.name=name
		new.variety=v
		new.size=size
	return new|OType

	function(PrimitiveOType t) PrimitiveOType:get_size -> int does
	return t.size

	function(PrimitiveOType t) PrimitiveOType:print -> void does
		printf("%i P %s\t", t.variety, datamodel::variety_to_name(t.variety))
		prints_tab(t.name, 30)
		printf("%i bytes\n", t:get_size()/8)
	return
endtype

type FPtrOType is packed
	int variety
	cstr name
	Project proj
	List args
	OType return_value

	function(cstr name, OType return_value, List args) FPtrOType::new -> OType does
		FPtrOType new = malloc(@sizeof(FPtrOType)@)|FPtrOType

		new.name=name
		new.args=args
		new.variety=F_PTR
		new.return_value=return_value
	return new|OType

	function(cstr name, OType return_value) FPtrOType::new_varargs -> OType does
		FPtrOType new = malloc(@sizeof(FPtrOType)@)|FPtrOType

		new.name=name
		new.args=null|List
		new.variety=F_PTR
		new.return_value=return_value
	return new|OType

	function(FPtrOType self) FPtrOType:get_size -> int does
	return self.proj.pointer_size

	function(FPtrOType t) FPtrOType:print -> void does
		printf("%i F %s\t", t.variety, datamodel::variety_to_name(t.variety))
		prints_tab(t.name, 30)
		printf("%i bytes \t->%s (", t:get_size()/8, t.return_value.name)
		if t.args|ptr==null do
			printf("...")
		else do
			int i=0
			while i<t.args.len do
				printf("%s", t.args:get_OType(i).name)
				if i!=t.args.len-1 do
					printf(", ")
				done
				i+=1
			done
		done
		printf(")\n")
	return
endtype

type StructOType is packed
	int variety
	cstr name
	Project proj
	bool packed
	List fields

	function(cstr name) StructOType::new -> OType does
		StructOType new = malloc(@sizeof(StructOType)@)|StructOType

		new.name=name
		new.variety=STRUCT
		new.fields=List::new()
		new.packed=false
	return new|OType

	function(StructOType self, TypedVar new_field) StructOType:add_field -> void does
		int size = new_field.type_:get_size()/8
		if new_field.type_:is_struct() do
			size = self.proj.pointer_size/8
		done

		int curr_offset=self:get_size()/8

		int pad = size-(curr_offset%size)
		if pad==size do
			pad = 0
		done
		# printf("adding=%s, curr_offset=%i, size=%i, pad=%i\n", decl_string, curr_offset, size, pad)

		if (pad!=0) & (-self.proj.options:contains("no_pad_struct")) & (-self.packed) do
			# printf("%s: packed=%i, pad=%i, f=%s\n", self.name, self.packed|int, pad, decl_string)
			# printf(self.name+": add "+new_field.name+" sz="+size:str()+" at "+curr_offset:str()+" : pad by "+pad:str()+"\n")
			int i=0
			while i<pad do
				self.fields:append_TypedVar(TypedVar::new("$"+(curr_offset+i):str(), self.proj:get_type("byte")))
				i+=1
			done
		done

		self.fields:append_TypedVar(new_field)
	return

	function(StructOType self, cstr field_name) StructOType:get_field_offset -> int does
		int i=0
		int sum=0
		while i<self.fields.len do
			if self.fields:get_TypedVar(i).name==field_name do
				return sum
			else do
				if self.fields:get_TypedVar(i).type_:is_struct() do
					sum+=self.proj.pointer_size
				else do
					sum+=self.fields:get_TypedVar(i).type_:get_size()
				done
			done
			i+=1
		done
	return -1

	function(StructOType self, cstr field_name) StructOType:has_field -> bool does
		int i=0
		while i<self.fields.len do
			if self.fields:get_TypedVar(i).name==field_name do
				return true
			done
			i+=1
		done
	return false

	function(StructOType self, cstr field_name) StructOType:get_field_index -> int does
		int i=0
		while i<self.fields.len do
			if self.fields:get_TypedVar(i).name==field_name do
				return i
			done
			i+=1
		done
		shoc::bail_out("(idx) No filed "+field_name+" in type "+self.name)
	return -1

	function(StructOType self, cstr field_name) StructOType:get_field_type -> OType does
		int i=0
		while i<self.fields.len do
			if self.fields:get_TypedVar(i).name==field_name do
				return self.fields:get_TypedVar(i).type_
			done
			i+=1
		done
		shoc::bail_out("(type) No field "+field_name+" in type "+self.name)
	return null|OType

	function(StructOType self) StructOType:get_size -> int does
		int i=0
		int sum=0
		while i<self.fields.len do
			if self.fields:get_TypedVar(i).type_:is_struct() do
				sum+=self.proj.pointer_size
			else do
				sum+=self.fields:get_TypedVar(i).type_:get_size()
			done
			i+=1
		done
	return sum

	function(StructOType t) StructOType:print -> void does
		printf("%i S %s\t", t.variety, datamodel::variety_to_name(t.variety))
		prints_tab(t.name, 30)
		printf("%i bytes \t{", t:get_size()/8)
		int i=0
		while i<t.fields.len do
			printf("%s: %s @ %i", t.fields:get_TypedVar(i).name, t.fields:get_TypedVar(i).type_.name, t:get_field_offset(t.fields:get_TypedVar(i).name)/8)
			if i!=t.fields.len-1 do
				printf(", ")
			done
			i+=1
		done
		printf("}\n")
	return
endtype

import generics

function(Project p) shoc::datamodel::init_base -> void does
	VOID=0
	S_INT=1
	U_INT=2
	FLOAT=3
	POINTER=4
	F_PTR=5
	STRUCT=6
	PTYPE=7
	SYMTYPE=8

	p:add_Type(PrimitiveOType::new("int", S_INT, 4*8))
	p:add_Type(PrimitiveOType::new("short", S_INT, 2*8))
	p:add_Type(PrimitiveOType::new("byte", S_INT, 1*8))
	p:add_Type(PrimitiveOType::new("long", S_INT, 8*8))
	p:add_Type(PrimitiveOType::new("xlong", S_INT, 16*8))

	p:add_Type(PrimitiveOType::new("uint", U_INT, 4*8))
	p:add_Type(PrimitiveOType::new("ushort", U_INT, 2*8))
	p:add_Type(PrimitiveOType::new("ubyte", U_INT, 1*8))
	p:add_Type(PrimitiveOType::new("ulong", U_INT, 8*8))
	p:add_Type(PrimitiveOType::new("uxlong", U_INT, 16*8))

	p:add_Type(PrimitiveOType::new("float", FLOAT, 8*8))

	p:add_Type(PrimitiveOType::new("bool", U_INT, 8))

	p:add_Type(PrimitiveOType::new("ptr", POINTER, p.pointer_size))
	p:add_Type(PrimitiveOType::new("cstr", POINTER, p.pointer_size))

	p:add_Type(PrimitiveOType::new("void", VOID, 0))
return

function(Project p, cstr name) Project:get_type -> OType does
return p.types:get(name)|OType

type Method is
	cstr name
	Project proj
	List args
	bool varargs
	OType return_value
	OType ptr_type
	StrMap locals
	List gir_code
	bool is_extern
	int  references

	#args is list of TypedVar
	function(Project proj, cstr name, List args, bool varargs, OType return_value, bool is_extern) Method::new -> Method does
		Method new = malloc(@sizeof(Method)@)|Method
		new.proj=proj
		new.locals=StrMap::new()
		new.name=name
		new.is_extern=is_extern
		new.varargs=varargs

		new.args=List::new()
		new.gir_code=List::new()
		new.references=0

		List fptr_type_args=List::new()

		int i=0
		TypedVar tv
		while i<args.len do
			if -proj:can_resolve_TypeName(args:get_Node(i)|DeclExpr.d_type) do
				shoc::bail_out("Function `"+name+"` has a parameter with unknown type: "+shoc::mangle_name(args:get_Node(i)|DeclExpr.d_type))
			done
			tv=TypedVar::new(args:get_Node(i)|DeclExpr.d_name, proj:resolve_TypeName(args:get_Node(i)|DeclExpr.d_type))
			new.args:append_TypedVar(tv)
			fptr_type_args:append_OType(tv.type_)
			new.locals:set(tv.name, tv.type_|ptr)
			i+=1
		done

		new.return_value=return_value

		if new.varargs do
			new.ptr_type=FPtrOType::new_varargs("$fp_"+name, new.return_value)
		else do
			new.ptr_type=FPtrOType::new("$fp_"+name, new.return_value, fptr_type_args)
		done
		proj:add_Type(new.ptr_type)
	return new

	function(Method func, cstr name, OType v) Method:add_local -> void does
		func.locals:set(name, v|ptr)
	return

	function(Method func, cstr name) Method:has_local -> bool does
	return func.locals:contains(name)

	function(Method func, cstr name) Method:get_local -> OType does
	return func.locals:get(name)|OType

	function(Method func) Method:print -> void does
		prints("METH: ")
		prints_tab(func.name, 60)
		prints(" -> ")
		prints_tab(func.return_value.name, 20)
		prints("(")
		int i=0
		while i<func.args.len do
			printf("%s:%s", func.args:get_TypedVar(i).name, func.args:get_TypedVar(i).type_.name)
			if i!=func.args.len-1 do
				printf(", ")
			done
			i+=1
		done
		printf(") - %i uses\n", func.references)
	return

	function(Method func) Method:print_locals -> void does
		int k=0
		while k<func.locals:num_keys() do
			printf("\tLocal: %s - %s\n", func.locals:get_key_by_num(k), (func.locals:get_by_num(k)|OType).name)
			k+=1
		done
	return

	function(Method func) Method:print_gir -> void does
		int k=0
		while k<func.gir_code.len do
			printf("\tGIR: %03i - ", k)
			func.gir_code:get_GIRInst(k):print()
			k+=1
		done
	return
endtype

function(NameExpr name) shoc::mangle_name -> cstr does
	if name|Node:is_a("GenericName") do
		return name|GenericName.base_name+shoc::mangle_generics(name|GenericName.args)
	elif name|Node:is_a("AccessorExpr") do
		cstr sep = "$"
		if name|AccessorExpr.mode=="::" do
			sep="$$"
		done
		return shoc::mangle_name(name|AccessorExpr.object|NameExpr)+sep+shoc::mangle_name(name|AccessorExpr.field|NameExpr)
	done
return name.name

function(List g_args) shoc::mangle_generics -> cstr does
	cstr name = ".G_"
	int i = 0
	while i<g_args.len do
		name+="A"+shoc::mangle_name(g_args:get(i)|NameExpr)+"Z"
		if i!=g_args.len-1 do
			name+="."
		done
		i+=1
	done
return name

function(Project p, NameExpr n) Project:can_resolve_TypeName -> bool does
return p:resolve_TypeName_nullable(n)|ptr!=null

function(Project p, NameExpr n) Project:resolve_TypeName_nullable -> OType does
	if n|Node:is_a("GenericName") do
		GenericName gn = n|GenericName
		GType g_type = p.generic_types:get(gn.base_name)|GType
		GenericArgs args = GenericArgs::copy(g_type.arguments)
		int i = 0
		while i<gn.args.len do
			args:set(args:get_name_for_idx(i), p:resolve_TypeName(gn.args:get_Node(i)|NameExpr))
			i+=1
		done
		return PType::new(g_type, args)|OType
	done
return p:get_type(n|NameExpr.name)

function(Project p, NameExpr n) Project:resolve_TypeName -> OType does
	OType o = p:resolve_TypeName_nullable(n)
	if o|ptr==null do
		shoc::bail_out("cant resolve tn")
	done
return o