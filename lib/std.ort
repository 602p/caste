import std_extern

# extern(...) printf -> int
# extern(cstr str) puts -> int
# extern(cstr str) system -> int
# extern(...) sprintf -> int
# extern(...) scanf -> int
# extern(int amt) malloc -> ptr
# extern(int amt) usleep -> void
# extern(cstr a, cstr b) strcat -> cstr
# extern(ptr a, ptr b, int amt) memcpy -> void
# extern(ptr a, int l, int v) memset -> void
# extern(cstr a, cstr b) strcpy -> cstr
# extern(cstr a, cstr b, int c) strncpy -> void
# extern(cstr s) strlen -> int
# extern(cstr s) atoi -> int
# extern(cstr a, cstr b) strtod -> float
# extern(cstr a, cstr b) strcmp -> int
# extern(cstr a, cstr b, int c) strncmp -> int
# extern(...) free -> void
# extern(int c) exit -> int
# extern() clock -> long

function(cstr a, cstr b) cstr:__add__ -> cstr does
	cstr result = malloc(strlen(a)+strlen(b)+1)
	strcpy(result, a)
	strcat(result, b)
return result

function(cstr a, int b) cstr:__mul__ -> cstr does
	int sz = strlen(a)*b
	sz+=1
	cstr result = malloc(sz)
	int idx=b
	while b>0 do
		b-=1
		strcat(result, a)
	done
return result

function(bool a, bool b) bool:or -> bool does
	if a do
		return a
	done
return b

function(ptr a, int b) ptr:offset -> ptr does
return ((a|long)+(b|long))|ptr

function(cstr a, int b) cstr:offset -> cstr does
return (a|ptr):offset(b)|cstr

function(cstr a, cstr b) cstr:__eq__ -> bool does
	if a|ptr==b|ptr do
		return true
	done
return strcmp(a,b)==0

function(cstr a, cstr b) cstr:__ne__ -> bool does
return strcmp(a,b)!=0

function(ptr a, ptr b) ptr:__eq__ -> bool does
return (a|long)==(b|long)

function(ptr a, ptr b) ptr:__ne__ -> bool does
return (a|long)!=(b|long)

function(cstr s) cstr:free -> void does
	free(s)
return

function(cstr s) cstr:copy -> cstr does
	cstr n = malloc(s:len()+1)
	strcat(n, s)
return n

function(cstr s) cstr:int -> int does
return atoi(s)

function(cstr s) cstr:float -> float does
return strtod(s, null)

function(byte b, cstr s) byte:in -> bool does
	int i=0
	int len=s:len()
	while i<len do
		if s[i]==b do
			return 1|bool
		done
		i+=1
	done
return 0|bool

function(cstr s) cstr:len -> int does
return strlen(s)

function(cstr s, int start, int len) cstr:substr -> cstr does
	cstr res = malloc(len+1)|cstr
	strncpy(res, s:offset(start), len)
	res[len]=0|byte
return res

function(cstr s, cstr o, int l) cstr:equals_l -> bool does
	int i=0
	while i<l do
		if s[i]!=o[i] do
			return false
		done
		i+=1
	done
return true

function(cstr s, cstr o) cstr:startswith -> bool does
return s:equals_l(o, o:len())

function(cstr s, cstr t) cstr:find -> int does
	int pos = 0
	while pos<s:len() do
		if strncmp(t, s:offset(pos), t:len())==0 do
			return pos
		done
		pos+=1
	done
return -1

function(cstr s, cstr t) cstr:contains -> bool does
return s:find(t)!=-1

function(int i) int:str -> cstr does
	cstr str = malloc(50) #TODO: FIX
	sprintf(str, "%i", i)
return str

function(float i) float:str -> cstr does
	cstr str = malloc(50) #TODO: FIX
	sprintf(str, "%f", i)
return str

function(bool b) bool:str -> cstr does
return (b|int):str()

type _STDReadIntBox is packed
	int i
endtype

function(cstr prompt) read_int -> int does
	_STDReadIntBox box = malloc(@sizeof(int)@)|_STDReadIntBox
	printf(prompt)
	scanf("%d", box|ptr)
	int result=box.i
	free(box|ptr)
return result

function(cstr prompt) read_line -> cstr does
	cstr result = malloc(256)
	printf(prompt)
	scanf("%s", result) #Horrible and unsafe
return result

function(cstr c) ord -> int does
return c[0]|int

function(cstr c) bord -> byte does
return c[0]|byte

function(cstr s) prints -> void does
	printf("%s", s)
return

function(cstr s, int sz) prints_tab -> void does
	if s:len()>sz do
		prints(s)
		return
	done
	printf("%s%s", s, " "*(sz-s:len()))
return