import err

type File is
	cstr path
	cstr mode
	bool writable
	bool reached_eof
	bool closed
	ptr  fd
	ptr  write_buffer
	int  write_buffer_size
	int  write_buffer_pos

	function(cstr path, cstr mode) File::open -> File does
		File f = malloc(@sizeof(File)@)|File
		f.writable = mode:contains("w")
		f.fd=fopen(path, mode)

		if f.fd|ptr|int==0 do
			orth::fail("***FATAL fileio/File::open error, fopen->0***\n")
		done

		f.mode=mode
		f.path=path
		f.reached_eof=0|bool
		f.closed=0|bool

		if f.writable do
			f.write_buffer_size=1024*1024
			f.write_buffer_pos=0
			f.write_buffer=malloc(f.write_buffer_size)
		done
	return f

	function(cstr path) File::exists -> bool does
		ptr file=fopen(path, "r")
		if file==null do
			return false
		done
		fclose(file)
	return true

	function(File f) File:ready -> bool does
		if f.reached_eof==0|bool do
			return 1|bool
		done
	return f.writable

	function(File f) File:readline -> cstr does
		int BLOCK_SIZE=63

		cstr buf = malloc(BLOCK_SIZE)
		buf[0]=("\0")[0]
		int buf_sz = BLOCK_SIZE
		cstr new_buf
		int last_size=-1
		cstr ra_buf

		while buf:len()!=last_size do
			last_size=buf:len()
			fgets(buf:offset(buf:len()), BLOCK_SIZE, f.fd)
			if buf:len()==0 do
				f.reached_eof=true
				return buf
			elif buf[buf:len()-1]==ord("\n")|byte do
				return buf
			else do
				buf_sz+=BLOCK_SIZE
				ra_buf=malloc(buf_sz)
				memcpy(ra_buf, buf, buf_sz-BLOCK_SIZE)
				free(buf)
				buf=ra_buf
				#buf=realloc(buf, buf_sz)

				if buf|ptr|int==0 do
					printf("***FATAL fileio/File::readline error, realloc->0 for req_sz=%i***\n", buf_sz)
					exit(1)
				done
			done
		done

		f.reached_eof=1|bool
	return buf

	function(File f) File:readall -> cstr does
		cstr contents=f:readline()
		cstr new_contents
		while f:ready() do
			new_contents=contents+f:readline()
			contents:free()
			contents=new_contents
		done
	return contents

	function(File f) File:flush_buffer -> void does
		fwrite(f.write_buffer, f.write_buffer_pos, 1, f.fd)
		f.write_buffer_pos=0
	return

	function(File f, cstr s) File:write -> void does
		# fprintf(f.fd, "%s", s)

		int len = s:len()
		if len+f.write_buffer_pos>f.write_buffer_size-1 do
			f:flush_buffer()
		done

		if len>f.write_buffer_size-1 do
			fwrite(s, len, 1, f.fd)
		else do
			memcpy(f.write_buffer:offset(f.write_buffer_pos), s, len+1)
			f.write_buffer_pos+=len
		done
	return

	function(File f, cstr s) File:writeln -> void does
		f:write(s)
		f:write("\n")
	return

	function(File f) File:close -> void does
		f:flush_buffer()
		if f.writable do
			fflush(f.fd|ptr)
		done
		fclose(f.fd|ptr)
		f.closed=1|bool
	return

	function(File f) File:free -> void does
		if f.closed==0|bool do
			f:close()
		done
		free(f|ptr)
	return
endtype